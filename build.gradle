buildscript {
  dependencies {
    classpath libs.android.plugin
    classpath libs.kotlin.plugin.core
    classpath libs.kotlin.plugin.compose
    classpath libs.kotlin.ksp.plugin
    classpath libs.maven.publish.plugin
    classpath libs.dokka.plugin
    classpath libs.spotless.plugin
    classpath libs.kotlinx.binaryCompatibilityValidator
  }

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }
}

allprojects {
  version = property("VERSION_NAME") as String

  repositories {
    mavenCentral()
    google()
  }
}

subprojects {
  tasks.withType(Test).configureEach {
    testLogging {
      if (System.getenv("CI") == "true") {
        events = ["failed", "skipped", "passed"]
      }
      exceptionFormat "full"
    }
  }

  tasks.withType(JavaCompile).configureEach { task ->
    task.sourceCompatibility = '11'
    task.targetCompatibility = '11'
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile).configureEach { task ->
    task.kotlinOptions {
      jvmTarget = '11'
    }
  }

  plugins.withType(com.android.build.gradle.BasePlugin).configureEach {
    def android = extensions.getByName("android") as com.android.build.gradle.BaseExtension
    android.compileSdkVersion libs.versions.compileSdk.get().toInteger()
    android.defaultConfig {
      minSdkVersion libs.versions.minSdk.get().toInteger()
      targetSdkVersion libs.versions.compileSdk.get().toInteger()
    }
    android.compileOptions {
      sourceCompatibility JavaVersion.VERSION_11
      targetCompatibility JavaVersion.VERSION_11
    }
    android.lintOptions {
      checkDependencies true
      checkReleaseBuilds false // Full lint runs as part of 'build' task.
    }
    android.composeOptions {
      kotlinCompilerExtensionVersion = libs.androidx.compose.compiler.get().version
    }
  }

  apply plugin: 'com.diffplug.spotless'
  spotless {
    kotlin {
      target("src/**/*.kt")
      ktlint(libs.ktlint.core.get().version)
        .editorConfigOverride([
          'ktlint_standard_filename': 'disabled',
          // Making something an expression body should be a choice around readability.
          'ktlint_standard_function-expression-body': 'disabled',
          'ktlint_function_naming_ignore_when_annotated_with': 'Composable',
        ])
        .customRuleSets([
          libs.ktlint.composeRules.get().toString(),
        ])
      licenseHeaderFile(rootProject.file('gradle/license-header.txt'))
    }
  }

  plugins.withId('maven-publish') {
    publishing {
      repositories {
        maven {
          name = "installLocally"
          url = "${rootProject.buildDir}/localMaven"
        }
      }
    }
  }
}
